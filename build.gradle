plugins {
    id 'java'
    id 'maven-publish'
}

repositories {
    maven {
        url "https://libraries.minecraft.net/"
    }
    maven {
        url "https://repo.cleanroommc.com/releases"
    }
    maven {
        url "https://maven.outlands.top/releases"
    }
    mavenCentral()
}

group = 'top.outlands'
base.archivesName = 'foundation'
version = '0.16.0'
java.targetCompatibility(JavaVersion.VERSION_21)

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.javassist:javassist:3.30.2-GA'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.0'
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'zone.rong:imaginebreaker:2.1'
    implementation 'org.ow2.asm:asm:9.8'
    implementation "net.lenni0451:Reflect:1.5.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'com.google.guava:guava:33.4.8-jre'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

}

compileJava {
    options.compilerArgs += [
            '--add-exports=java.base/jdk.internal.reflect=ALL-UNNAMED',
            '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'
    ]
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching("*")
    }
}

tasks.register('testTools', Test) {
    group = 'verification'

    useJUnitPlatform()
    filter {
        includeTestsMatching("*")
    }

    maxHeapSize = '1G'

    testLogging {
        events "passed"
    }

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "cleanroom"
            url = "https://repo.cleanroommc.com/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = "foundation"
            version = project.version
            artifact sourcesJar
            from components.java
        }
    }
}